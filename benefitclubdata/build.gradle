plugins {
    id 'com.android.library'
    id 'kotlin-android'
    id 'kotlin-kapt'
    id 'maven-publish'
}

android {
    signingConfigs {
        debug_signing {
            keyAlias 'keyDebug'
            keyPassword 'PrismaDebug'
            storeFile file('debug_signing')
            storePassword 'PrismaDebug'
        }
        release_signing {
            keyAlias 'TodoPagoProd'
            keyPassword RELEASE_STORE_PASSWORD_PRISMA
            storeFile file('TodoPagoProdKeyStore')
            storePassword RELEASE_KEY_PASSWORD_PRISMA
        }
    }
    compileSdkVersion 30
    buildToolsVersion "30.0.3"

    defaultConfig {
        minSdkVersion 21
        targetSdkVersion 30
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles "consumer-rules.pro"
    }

    buildTypes {
        integracion {
            debuggable true
            resValue("string", "URL_BASE", "\"https://apis.integration.todopago.com.ar/\"")
        }

        qa {
            debuggable true
            resValue("string", "URL_BASE", "\"https://apis.wallet-qa.prismamp.com\"")
        }

        releaseDebug {
            signingConfig signingConfigs.debug_signing
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            minifyEnabled false
            debuggable true
            resValue("string", "URL_BASE", "\"https://apis.wallet.prismamp.com/\"")
        }

        release {
            signingConfig signingConfigs.debug_signing
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            minifyEnabled false
            debuggable false
            resValue("string", "URL_BASE", "\"https://apis.wallet.prismamp.com/\"")
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
}

afterEvaluate {
    publishing {
        publications {

            integracion(MavenPublication) {
                from components.integracion
                groupId = 'com.todopago.data'
                artifactId = 'integracionTodopagoData'
                version = android.defaultConfig.versionName
            }

            release(MavenPublication) {
                from components.release
                groupId = 'com.todopago.data'
                artifactId = 'releaseTodopagoData'
                version = android.defaultConfig.versionName
            }

            releaseDebug(MavenPublication) {
                from components.releaseDebug
                groupId = 'com.todopago.data'
                artifactId = 'releaseDebugTodopagoData'
                version = android.defaultConfig.versionName
            }

            qa(MavenPublication) {
                from components.qa
                groupId = 'com.todopago.data'
                artifactId = 'qaTodopagoData'
                version = android.defaultConfig.versionName
            }

        }
    }
}

dependencies {

    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    implementation 'androidx.core:core-ktx:1.5.0'
    implementation 'androidx.appcompat:appcompat:1.3.0'
    implementation 'com.google.android.material:material:1.3.0'
    implementation project(path: ':domain')
    testImplementation 'junit:junit:4.+'
    androidTestImplementation 'androidx.test.ext:junit:1.1.2'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.3.0'

    //Room
    implementation 'androidx.room:room-runtime:2.2.1'
    kapt 'androidx.room:room-compiler:2.2.1'
    implementation 'androidx.room:room-ktx:2.2.1'

    //region Network
    implementation "com.squareup.retrofit2:retrofit:2.9.0"
    implementation "com.squareup.retrofit2:converter-gson:2.9.0"
    implementation "com.squareup.retrofit2:adapter-rxjava2:2.9.0"
    implementation "com.squareup.okhttp3:logging-interceptor:4.9.0"
    //endregion

    // Koin for Android
    implementation "org.koin:koin-android:$koin_version"

    // Tools
    implementation 'com.facebook.stetho:stetho-okhttp3:1.5.1'

    // ******************************* CORE-DATA ********************************************
    def coreDataVersion = "2.0.2.0"

    integracionApi("com.prisma.data:integracionData:${coreDataVersion}")
    qaApi("com.prisma.data:qaData:${coreDataVersion}")
    releaseDebugApi("com.prisma.data:releaseDebugData:${coreDataVersion}")
    releaseApi("com.prisma.data:releaseData:${coreDataVersion}")
    // ******************************* CORE-DATA ********************************************
}